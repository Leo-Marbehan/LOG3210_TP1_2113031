options {
	JAVA_UNICODE_ESCAPE=true;
	STATIC=false;					// 1 seul parseur actif a la fois (+ rapide)
	MULTI=true;						// type noeud = f(nom noeud)
	VISITOR=true;					// Cree une methode accept(visiteur)
}

//
// PARSER DEFINITION
//

PARSER_BEGIN(Parser)

package analyzer.ast;

public class Parser
{
	public static ASTProgram ParseTree(java.io.InputStream input) throws ParseException
	{
		Parser c = new Parser(input);
		return c.Program();
	}
}

PARSER_END(Parser)

//
// LEXICAL ANALYSIS
//

// White space

SKIP :
{
	< ( " " | "\t" | "\n" | "\r" )+	>
}

// Comments

MORE :
{
  "//" : IN_SINGLE_LINE_COMMENT
|
  <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT
|
  "/*" : IN_MULTI_LINE_COMMENT
}

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : DEFAULT
}

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
  <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}




// Keywords
TOKEN :
{
	< IF: "if"	> |
	< ELSE: "else" > |
	< STRING: "string"> |
	< INT: "int"> |
	< FLOAT: "float" > |
	< BOOL: "bool"> |
	< PRINT: "print" > |
	< INPUT: "input" > |
	< IN: "in" > |
	< DO: "do" > |
	< WHILE: "while" > |
	< SWITCH: "switch" > |
	< CASE: "case" > |
    < DEF: "default">|
    < FOR: "for"> |
    < FUNC: "func"> |
    < RET: "return">
	// À compléter au besoin
}

// Operators
TOKEN :
{
	< ASSIGN: < EQUAL >  > |
	< COMPARE: < LESS > | < LESSEQUAL > | < GREAT > | <GREATEQUAL > | < DIFF > | < EQUALEQUAL > > |
    < LOGIC: < AND > | < OR > > |
	< #LESS: "<" > |
	< #LESSEQUAL: "<=" > |
	< #GREAT: ">" > |
	< #GREATEQUAL: ">=" > |
	< #DIFF: "!=" > |
	< #EQUALEQUAL: "==" > |

	< PLUS: "+" > |
	< MINUS: "-" > |
	< FOIS: "*" > |
	< DIV: "/" > |
    < NOT: "!" > |

	< #EQUAL: "=" > |
	< LPAREN: "(" > |
	< RPAREN: ")" > |
	< LACC: "{" > |
	< RACC: "}" > |
	< COLON: ":" > |
	< SEMICOLON: ";" > |
	< COMMA: "," > |
	< #AND: "&&" > |
	< #OR: "||" >
	// À compléter au besoin
}


// Identifiers and numbers
TOKEN :
{
	< IDENTIFIER: <LETTER> ( <LETTER> | <DIGIT> )* > |
	< #LETTER: ["a"-"z","A"-"Z","_"] > |
	< #DIGIT: ["0"-"9"] > |
	< INTEGER: ["1"-"9"] (["0"-"9"])* | "0" > |
    < #EXPONENT: ("e" | "E") (<PLUS> | <MINUS>) <INTEGER> > |
    < #DECIMAL: <INTEGER> (<EXPONENT>)? > |
    < REAL: ("." <DECIMAL>) | ((<INTEGER>)  "." (<DECIMAL>)?)>
}





//
// SYNTAX ANALYSIS
//

ASTProgram Program() : { }
{
	Block() <EOF> { return jjtThis;  }
}

void Block() #void : { }
{
	 ( Stmt() )*
}


void Stmt() #void : { }
{
	AssignStmt() <SEMICOLON> |
	WhileStmt() |
	DoWhileStmt() |
	IfStmt() |
	ForStmt() |
	FunctionStmt()
}

void InFunctionBlock() #void : { }
{
    ( InFunctionStmt() )* [ReturnStmt()]
}

void InFunctionStmt() #void : { }
{
    AssignStmt() <SEMICOLON> |
    InFunctionWhileStmt() |
    InFunctionDoWhileStmt() |
    InFunctionIfStmt() |
    InFunctionForStmt()
}


void BlockStmt() #void : { }
{
    <LACC> Block() <RACC> |
    Stmt()
}

void InFunctionBlockStmt() #void : { }
{
    <LACC> InFunctionBlock() <RACC> |
    InFunctionStmt() |
    ReturnStmt()
}

void WhileStmt() : { Token t;}
{
    <WHILE> <LPAREN> Expr()#WhileCond <RPAREN> BlockStmt()#WhileBlock
}

void InFunctionWhileStmt() #WhileStmt : { Token t;}
{
    <WHILE> <LPAREN> Expr()#WhileCond <RPAREN> InFunctionBlockStmt()#WhileBlock
}

void DoWhileStmt() : { Token t;} {
    <DO> <LACC> Block()#WhileBlock <RACC> <WHILE> <LPAREN> Expr()#WhileCond <RPAREN> <SEMICOLON>
}

void InFunctionDoWhileStmt() #DoWhileStmt : { Token t;} {
    <DO> <LACC> InFunctionBlock()#WhileBlock <RACC> <WHILE> <LPAREN> Expr()#WhileCond <RPAREN> <SEMICOLON>
}

void IfStmt() : { Token t;}
{
    <IF> <LPAREN> Expr()#IfCond <RPAREN> BlockStmt()#IfBlock [LOOKAHEAD(2) <ELSE> BlockStmt()#ElseBlock]
}

void InFunctionIfStmt() #IfStmt : { Token t;}
{
    <IF> <LPAREN> Expr()#IfCond <RPAREN> InFunctionBlockStmt()#IfBlock [LOOKAHEAD(2) <ELSE> InFunctionBlockStmt()#ElseBlock]
}

void ForStmt() : { Token t;}
{
    <FOR> <LPAREN> [AssignStmt()] <SEMICOLON> [Expr()] <SEMICOLON> [AssignStmt()] <RPAREN> BlockStmt()
}

void InFunctionForStmt() #ForStmt : { Token t;}
{
    <FOR> <LPAREN> [AssignStmt()] <SEMICOLON> [Expr()] <SEMICOLON> [AssignStmt()] <RPAREN> InFunctionBlockStmt()
}

void ReturnStmt() : { Token t;}
{
    <RET> [Expr()] <SEMICOLON>
}

void FunctionStmt() : { Token t;}
{
    <FUNC> Identifier() <LPAREN> [Identifier() (<COMMA> Identifier())*] <RPAREN> <LACC> InFunctionBlock() <RACC>
}

void AssignStmt() : { Token t;}
{
	Identifier() <ASSIGN> Expr()
}


void Expr() #void : {}
{
    LogicExpr()
}

void BasicExpr() #void : {}
{
	Identifier() |
	IntValue() |
	RealValue()
}

void ParenExpr() #void : {}
{
    <LPAREN> Expr() <RPAREN> |
    BasicExpr()
}

void NotExpr() #void : {}
{
    <NOT> NotExpr() #Not |
    ParenExpr()
}

void NegExpr() #void : {}
{
    <MINUS> NegExpr() #Minus |
    NotExpr()
}

void MulExpr() #Multi(>1) : {}
{
    NegExpr() (<FOIS> NegExpr())*
}

void DivExpr() #Multi(>1) : {}
{
    MulExpr() (<DIV> MulExpr())*
}

void AddExpr() #Addition(>1) : {}
{
    DivExpr() (<PLUS> DivExpr())*
}

void SubExpr() #Addition(>1) : {}
{
    AddExpr() (<MINUS> AddExpr())*
}

void CompareExpr() #Compare(>1) : {}
{
    SubExpr() (<COMPARE> SubExpr())*
}

void LogicExpr() #Logic(>1) : {}
{
    CompareExpr() (<LOGIC> CompareExpr())*
}






void Identifier() : { Token t;}
{
	t = <IDENTIFIER> {jjtThis.setValue(t.image);}
}

void IntValue() : { Token t;}
{
	t = <INTEGER> {jjtThis.setValue(Integer.parseInt(t.image));}
}

void RealValue() : { Token t;}
{
	t = <REAL> {jjtThis.setValue(Double.parseDouble(t.image));}
}
